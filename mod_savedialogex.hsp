#module

#uselib "comdlg32"
#func GetSaveFileName "GetSaveFileNameA" int

//拡張ファイル保存ダイアログ
//savedialogex p1,p2,p3,p4
//p1：ファイルの種類
//p2：標準ファイル名
//p3：ダイアログのタイトル
//p4：初期フォルダパス
//ファイル保存ダイアログの拡張版です。標準ファイル名やダイアログのタイトル、初期フォルダのパスが指定できます。
//p1にはファイルの種類を指定しますが、dialog命令と記法が違うので注意してください。
//　テキストファイル(*.txt)|*.txt|カンマ区切りファイル(*.csv)|*.csv|…
//のように、表示名と拡張子を|で区切って記述します。
//正しく選択されると、statに1が、refstrにファイルパスが返ります。
//キャンセルされると、statに0が返ります。
#deffunc savedialogex str v1, str v2, str v3, str v4

	sdim lpstrFilter, 256 : lpstrFilter = v1
	sdim lpstrInitialDir, 128 : lpstrInitialDir = v4
	sdim lpstrFile, 128 : lpstrFile = v2
	sdim lpstrTitle, 64 : lpstrTitle = v3

	repeat strlen (lpstrFilter)
		x = peek (lpstrFilter, cnt)
		if x>$80 & x<$A0 | x>$DF & x<$FD : continue cnt + 2
		if x = $7C : poke lpstrFilter, cnt, $00
	loop

	prm = 76, hwnd, hinstance, varptr (lpstrFilter), 0, 0, 0, varptr (lpstrFile), 256, 0, 0, varptr (lpstrInitialDir), varptr (lpstrTitle)
	prm.13 = $806

	GetSaveFileName varptr (prm)

	if stat = 0 : return
	return lpstrFile

#global
